buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.palominolabs.gradle.task:gradle-git-clone-task:0.0.3'
    }
}
plugins {
  id "com.matthewprenger.cursegradle" version "1.0.7"
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the nessasary things for Forge to be setup.


version = "1.0"

def getGitRef() {
  try {
    def stdout = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'rev-parse', '--short', 'HEAD'
      standardOutput = stdout
    }
    return stdout.toString().trim()
  } catch (Throwable e) {
    return "unknown"
  }
}

if (System.getenv("PROMOTED_NUMBER") != null)
    ext.buildNumber = "${System.getenv("PROMOTED_NUMBER")}"
else if (System.getenv("BUILD_NUMBER") != null)
    ext.buildNumber = "${System.getenv("BUILD_NUMBER")}"
else
    ext.buildNumber = "+" + getGitRef()

//if (System.getenv().BUILD_NUMBER != null)
//	ext.buildNumber = System.getenv().BUILD_NUMBER?.toInteger()
//else
//	ext.buildNumber = 0

group= "pcl.opensecurity" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "OpenSecurity-MC1.12.2"

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20180701"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    replace "@VERSION@", project.version
	replace "@BUILD@", project.ext.buildNumber
}
jar.classifier = "${ext.buildNumber}"
jar.manifest = manifest { attributes(
	"FMLCorePlugin": "pcl.opensecurity.util.SoundUnpack",
	"FMLCorePluginContainsFMLMod": "icanhazfmlmodpweeze"
) }
repositories {
    maven {
        name = "OpenComputers"
        url = "http://maven.cil.li/"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
}

def openComputersVersion = "MC1.12.2-1.7.+"
def jei_version = "1.12.2:4.9.+"
def ae2Version = "rv6-stable-1"
def projredVersion="1.12.2-4.9.0.86"
def MCMPVersion = "2.5.3"

dependencies {
    deobfCompile "li.cil.oc:OpenComputers:${openComputersVersion}"
    compile 'org.mindrot:jbcrypt:0.3m'
    deobfCompile "advanced-rocketry:AdvancedRocketry-1.12.2-1.5.0:127:deobf"
    compile fileTree(include: ['*.jar'], dir: 'libs')
    deobfCompile "mezz.jei:jei_${jei_version}:api"
    // at runtime, use the full JEI jar
    runtime "mezz.jei:jei_${jei_version}"
    // runtime "libVulpes:libVulpes-1.12.2-0.4.0:51:deobf"

    // all those just for the case block =>
    provided ("appeng:appliedenergistics2:${ae2Version}:api") {
        transitive = false
    }

    provided ("mrtjp:ProjectRed:${projredVersion}:Base") {
        exclude module: "NotEnoughItems"
        exclude module: "CodeChickenLib"
        exclude module: "jei_1.12"
        exclude module: "ForgeMultipart"
    }
}

def curseforge_key = 0
def curse_changlog = "default"
if (project.hasProperty('curseforge_key')) {
	curseforge_key = project.curseforge_key
	curse_changlog = project.promochangelog
}

curseforge {
	apiKey = curseforge_key
	project {
		id = "231687"
		releaseType = "release"
		changelog = curse_changlog
	}
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version + "-" + project.ext.buildNumber, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}


task syncGitWiki(type: com.palominolabs.gradle.task.git.clone.GitCloneTask) {
    dir = file("src/main/resources/assets/opensecurity/doc")
    uri = 'https://github.com/PC-Logix/OpenSecurity.wiki.git'
    treeish = "origin/master"
    forceFetch = true
}

